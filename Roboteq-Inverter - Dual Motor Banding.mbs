option explicit

' Variables
' ======================

dim debug as boolean
debug = false

' User input
dim throttleCmd as integer 	' Throttle command
dim brakeCmd as integer		' Brake command

' Sensed Values
dim motorSpeed_1 as integer	' Measured motor speed
dim motorSpeed_2 as integer	' Motor 2
dim prevMotorSpeed_1 as integer	' Measured motor speed prev loop
dim prevMotorSpeed_2 as integer	' Motor 2

' Calculated values
dim neutralDuty_1 as integer
dim neutralDuty_2 as integer

dim command_1 as integer		' Command to apply to neutral duty
dim command_2 as integer		' Motor 2

dim commandDuty_1 as integer    ' Final command to motors
dim commandDuty_2 as integer    ' Final command to motors

' Hard limits
dim maxDuty as integer 		' Max duty cycle allowable
dim minDuty as integer 		' Min duty cycle allowable
dim startDrive as boolean	' Start drive bool recieved from CC

' Banding values
dim dutyCycleMaxBand_1 as integer ' max duty cycle can jump from current neutral duty
dim dutyCycleMaxBand_2 as integer ' max duty cycle can jump from current neutral duty
dim dutyCycleMinBand_1 as integer ' min duty cycle band, max regen value
dim dutyCycleMinBand_2 as integer ' min duty cycle band, max regen value

' Side gains
dim neutralGain as integer 		' Neutral duty cycle gain

' Set variables
' ======================
maxDuty = 850
minDuty = 30

dutyCycleMaxBand_1 = 300  ' Duty cycle accel band
dutyCycleMaxBand_2 = dutyCycleMaxBand_1
dutyCycleMinBand_1 = 50   ' Duty cycle regen brake band
dutyCycleMinBand_2 = dutyCycleMinBand_1

prevMotorSpeed_1 = 0
prevMotorSpeed_2 = 0

neutralGain = 100 		' Neutral gain multiplier

SetCommand(_B,1,0)      ' set start drive to 0
SetCommand(_VAR,1,0)	' set throttle command to 0
SetCommand(_VAR, 2,0)	' set brake val to 0


' Start loop
' ==========
top:

' Start Drive
startDrive = getvalue(_B, 1) ' get start drive bool
'print("startDrive=",startDrive,"\r")

if (startDrive = 0)
 'print("startDrive=0 \r")
 wait(10)
 goto top
end if

' Get commands from user variables
motorSpeed_1 = getvalue(_BS, 1)     ' get motor speed 1
motorSpeed_2 = getvalue(_BS, 2)     ' get motor speed 2

throttleCmd = getvalue(_VAR, 1)	' Throttle command (user variable #1)

' Clean motor speed
motorSpeed_1 = abs(motorSpeed_1)	' ensure positive
motorSpeed_2 = abs(motorSpeed_2)    ' ensure positive 

if (abs(motorSpeed_1-prevMotorSpeed_1)>3000) ' if motor speed diff is greater than x
 ' use previous loop motor speed
 motorSpeed_1 = prevMotorSpeed_1
end if

if (abs(motorSpeed_2-prevMotorSpeed_2)>3000) ' if motor speed diff is greater than x
 ' use previous loop motor speed
 motorSpeed_2 = prevMotorSpeed_2
end if

if (debug)
 print("motorSpeed_1=",motorSpeed_1,"motorSpeed_2=",motorSpeed_2,"\r")
end if

' Calculate neutral duty for each motor
neutralDuty_1 = (2177*neutralGain*motorSpeed_1 - 5303)/1000000
neutralDuty_2 = (2177*neutralGain*motorSpeed_2 - 5303)/1000000
if (debug)
 print("neutralDuty_1=",neutralDuty_1,"neutralDuty_2=",neutralDuty_2,"\r")
end if

' Calculate Arbitrated
command_1 = (((dutyCycleMaxBand_1+dutyCycleMinBand_2)*throttleCmd)/1000)-dutyCycleMinBand_1
command_2 = (((dutyCycleMaxBand_1+dutyCycleMinBand_2)*throttleCmd)/1000)-dutyCycleMinBand_2

' Calculate final command
commandDuty_1 = neutralDuty_1 + command_1
commandDuty_2 = neutralDuty_2 + command_2

if (debug)
 print("command_1=",command_1, "command_2=",command_2,"commandDuty_1=",commandDuty_1,"commandDuty_2=",commandDuty_2,"\r")
end if
 
' Hard safety limits 
' ==================

' Ensure duty cycle command is within tollerable allowances. Max and min
if (commandDuty_1 < minDuty)
 commandDuty_1 = 0
elseif (commandDuty_1 > maxDuty)
 commandDuty_1 = maxDuty
end if

if (commandDuty_2 < minDuty)
 commandDuty_2 = 0
elseif (commandDuty_2 > maxDuty)
 commandDuty_2 = maxDuty
end if

' Limit command to 1000 max
if (commandDuty_1 > 1000)
 commandDuty_1 = 1000
end if

if (commandDuty_2 > 1000)
 commandDuty_2 = 1000
end if

if (debug)
 print("commandDutyFinal_1=",commandDuty_1,"commandDutyFinal_2=",commandDuty_2,"\r")
end if
setcommand(_G, 1, commandDuty_1) ' Apply power to the motor
setcommand(_G, 2, commandDuty_2) ' Apply power to the motor

wait(1)     ' wait 1ms
goto top 'repeat loop forever
