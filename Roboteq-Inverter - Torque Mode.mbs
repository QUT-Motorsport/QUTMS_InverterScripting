option explicit

' Variables
' ======================

dim debug as boolean
debug = false

' User input
dim throttleCmd as integer 	' Throttle command
dim brakeCmd as integer		' Brake command

' Sensed values
dim motorSpeed_1 as integer	' Measured motor speed
dim motorSpeed_2 as integer	' Motor 2
dim prevMotorSpeed_1 as integer	' Measured motor speed prev loop
dim prevMotorSpeed_2 as integer	' Motor 2

' Calculated values
dim commandDuty_1 as integer    ' Final command to motors
dim commandDuty_2 as integer    ' Final command to motors

' Hard limits
dim startDrive as boolean		' Start drive bool recieved from CC
dim maxAccel as integer			' Max torque value. 1000 max 0 min
dim maxRegen as integer			' Max regen value 1000 max 0 min
dim minRegenSpeed as integer	' Min regen speed
dim brakeThreshold as integer 	' Threshold at which the brake will cut off. 0 min 1000 max. % * 10


' Set variables
' ======================
maxAccel = 1000     ' Duty cycle accel band
maxRegen = 100       ' Duty cycle regen brake band
minRegenSpeed = 590		' The minimum allowable RPM to use regen braking

brakeThreshold = 350	' decimal fraction * 1000

prevMotorSpeed_1 = 0
prevMotorSpeed_2 = 0

SetCommand(_B,1,0)      ' set start drive to 0
SetCommand(_VAR,1,0)	' set throttle command to 0
SetCommand(_VAR, 2,0)	' set brake val to 0


' Start loop
' ==========
top:

' Start Drive
startDrive = getvalue(_B, 1) ' get start drive bool
'print("startDrive=",startDrive,"\r")

if (startDrive = 0)
 if (debug)
  print("startDrive=0 \r")
 end if
 wait(10)
 goto top
end if

' Get commands from user variables
motorSpeed_1 = getvalue(_BS, 1)     ' get motor speed 1
motorSpeed_2 = getvalue(_BS, 2)     ' get motor speed 2

throttleCmd = getvalue(_VAR, 1)	' Throttle command (user variable #1)
brakeCmd = getvalue(_VAR, 2)	' Brake command (user variable #2)

' Clean motor speed
motorSpeed_1 = abs(motorSpeed_1)	' ensure positive
motorSpeed_2 = abs(motorSpeed_2)    ' ensure positive 

if (abs(motorSpeed_1-prevMotorSpeed_1)>3000) ' if motor speed diff is greater than x
 ' use previous loop motor speed
 motorSpeed_1 = prevMotorSpeed_1
end if

if (abs(motorSpeed_2-prevMotorSpeed_2)>3000) ' if motor speed diff is greater than x
 ' use previous loop motor speed
 motorSpeed_2 = prevMotorSpeed_2
end if

if (debug)
 print("motorSpeed_1=",motorSpeed_1,"motorSpeed_2=",motorSpeed_2,"\r")
end if

' Calculate throttle value
commandDuty_1 = (((maxAccel + maxRegen)*throttleCmd)/1000)-maxRegen
commandDuty_2 = (((maxAccel + maxRegen)*throttleCmd)/1000)-maxRegen

' if brake, regen brake
if (brakeCmd > brakeThreshold)
	commandDuty_1 = (-1 * maxRegen)
	commandDuty_2 = (-1 * maxRegen)
	if (debug)
		print("brake active, no throttle\r")
	end if
end if

' No regen if rpm less than min rpm
if (motorSpeed_1 < minRegenSpeed And commandDuty_1 < 0)
 commandDuty_1 = 0
end if

if (motorSpeed_2 < minRegenSpeed And commandDuty_2 < 0)
 commandDuty_2 = 0
end if


if (debug)
 print("calculatedCommandDuty_1=",commandDuty_1," calculatedCommandDuty_2=",commandDuty_2,"\r")
end if
 

' Hard safety limits 
' ==================
 
' Ensure duty cycle command is within tollerable allowances. Max andi min
if (commandDuty_1 < (-1 * maxRegen))
 commandDuty_1 = 0
elseif (commandDuty_1 > maxAccel)
 commandDuty_1 = maxAccel
end if

if (commandDuty_2 < (-1 * maxRegen))
 commandDuty_2 = 0
elseif (commandDuty_2 > maxAccel)
 commandDuty_2 = maxAccel
end if

' Limit command to 1000 max
if (commandDuty_1 > 1000)
 commandDuty_1 = 1000
end if

if (commandDuty_2 > 1000)
 commandDuty_2 = 1000
end if

if (debug)
 print("commandDutyFinal_1=",commandDuty_1,"commandDutyFinal_2=",commandDuty_2,"\r")
end if
setcommand(_G, 1, commandDuty_1) ' Apply power to the motor
setcommand(_G, 2, commandDuty_2) ' Apply power to the motor

wait(1)     ' wait 1ms
goto top 'repeat loop forever

